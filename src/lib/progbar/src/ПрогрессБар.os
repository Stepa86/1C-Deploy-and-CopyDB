///////////////////////////////////////////////////////////////////////////////
// Класс реализации прогресс бара консольных приложений
//
// (C) Maximov Valеry aka TheShadowCo
///////////////////////////////////////////////////////////////////////////////

Перем Консоль;
Перем ТекстПодсказки;
Перем НадоВыводитьПроценты;
Перем НадоВыводитьНомерИКоличество;
Перем НадоВыводитьВремя;
Перем КоличествоШаговПрогресса;
Перем ЦветТекстаКонсоли;

Перем ПоследнийШаг;

Перем Инициализирован;
Перем ДатаНачала;
Перем Длительность;

///////////////////////////////////////////////////////////////////////////////

// Начать
//	Выполняет инициализацию прогресс бара
//
// Параметры:
//  КоличествоШагов 			- Число 	- Общее количество шагов прогресса
//	Подсказка 					- Строка 	- Текст, выводимый перед строкой прогресса
//	ВыводитьПроценты 			- Булево 	- Флаг отображения прогресса в процентах (в конце строки)
//	ВыводитьНомерИКоличество 	- Булево 	- Флаг вывода прогресса в абсолютном виде
//	ВыводитьВремя 				- Булево 	- Флаг вывода времени выполнения и прогноза завершения
//
Процедура Начать(КоличествоШагов, Подсказка = "", ВыводитьПроценты = ИСТИНА, ВыводитьНомерИКоличество = ИСТИНА, ВыводитьВремя = ИСТИНА) Экспорт
	
	Консоль = Новый Консоль;
	ЦветТекстаКонсоли = Консоль.ЦветТекста;
	ТекстПодсказки = Подсказка;
	НадоВыводитьПроценты = ВыводитьПроценты;
	НадоВыводитьНомерИКоличество = ВыводитьНомерИКоличество;
	НадоВыводитьВремя = ВыводитьВремя;
	КоличествоШаговПрогресса = Число(КоличествоШагов);
	Если КоличествоШаговПрогресса < 0 Тогда
		
		ВызватьИсключение "Количество шагов должно быть больше 0";
		
	КонецЕсли;
	
	ПоследнийШаг = 0;
	ДатаНачала = ТекущаяДата();
	Длительность = 0;
	Инициализирован = ИСТИНА;
	
КонецПроцедуры // Начать

// СделатьШаг
//	Двигает строку прогресса на шаг в перед. Предоставляется возможность управлять
//	размером шага, общим количеством и подсказкой
//
// Параметры:
//	Инкремент 			- Число 	- Размер и направление (для отрицательных значений) шага
//	Подсказка 			- Строка 	- Текст, выводимый перед строкой прогресса
//  КоличествоШагов 	- Число 	- Общее количество шагов прогресса
//
Процедура СделатьШаг(Инкремент = 1, КоличествоШагов = 0, Подсказка = Неопределено) Экспорт
	
	Если Не Инициализирован Тогда
		
		ВызватьИсключение "Прогресс бар не инициализирован. Воспользуйтесь методом Начать";
		
	КонецЕсли;
	
	Если КоличествоШагов > 0 Тогда
		
		КоличествоШаговПрогресса = КоличествоШагов;
		
	КонецЕсли;
	
	ПоследнийШаг = ПоследнийШаг + Инкремент;
	Если ПоследнийШаг < 0 Тогда
		
		ПоследнийШаг = 0;
		
	ИначеЕсли ПоследнийШаг > КоличествоШаговПрогресса Тогда
		
		ПоследнийШаг = КоличествоШаговПрогресса;
		
	КонецЕсли;
	
	Если Подсказка <> Неопределено Тогда
		
		ТекстПодсказки = Подсказка;
		
	КонецЕсли;
	
	Консоль.ВидимостьКурсора(Ложь);	
	Консоль.КурсорЛево = 0;
	Для Ит = 1 По Консоль.Ширина - 1 Цикл

		Консоль.Вывести(" ");

	КонецЦикла;
	Консоль.КурсорЛево = 0;
	
	Если ЗначениеЗаполнено(ТекстПодсказки) Тогда
		
		Консоль.ЦветТекста = ЦветКонсоли.Green;
		Консоль.Вывести(ТекстПодсказки);
		
	КонецЕсли;
	
	Если НадоВыводитьНомерИКоличество Тогда
		
		Консоль.ЦветТекста = ЦветКонсоли.Yellow;
		Консоль.Вывести(СтрШаблон("(%1 из %2) ", ПоследнийШаг, КоличествоШаговПрогресса));
		
	КонецЕсли;
	
	Консоль.ЦветТекста = ЦветКонсоли.Magenta;
	
	Позиция = Цел(ПоследнийШаг / КоличествоШаговПрогресса * 100 / 2);
	
	Значек = Сред("/-\|", ПоследнийШаг % 4 + 1, 1);
	СтрокаЗаполненногоПрогресса = Сред("--------------------------------------------------", 1, Позиция);
	СтрокаНеЗаполненногоПрогресса = Сред("                                                  ", Позиция);
	
	Консоль.Вывести(СтрШаблон("%1 |%2%3| ", Значек, СтрокаЗаполненногоПрогресса, СтрокаНеЗаполненногоПрогресса));
	
	Если НадоВыводитьПроценты Тогда
		
		Консоль.ЦветТекста = ЦветКонсоли.Yellow;
		Консоль.Вывести("" + Цел(ПоследнийШаг / КоличествоШаговПрогресса * 100) + "% ");
		
	КонецЕсли;
	
	Длительность = ТекущаяДата() - ДатаНачала;

	Если НадоВыводитьВремя Тогда	
		
		ДлительностьСтрока = ВремяВСтроку(Длительность);
		Прогноз = (Длительность * КоличествоШаговПрогресса) / ПоследнийШаг - Длительность;
		ПрогнозСтрока = ВремяВСтроку(Прогноз);
		Консоль.ЦветТекста = ЦветКонсоли.Yellow;
		Консоль.Вывести("t ");
		Консоль.ЦветТекста = ЦветКонсоли.Cyan;
		Консоль.Вывести("" + ДлительностьСтрока + " ~" + ПрогнозСтрока);
		
	КонецЕсли;
	
	Консоль.ВидимостьКурсора(Истина);
	Консоль.ЦветТекста = ЦветТекстаКонсоли;
	
КонецПроцедуры // СделатьШаг

// Завершить
//	Завершает вывод строки прогресс бара
//
Процедура Завершить() Экспорт
	
	Инициализирован = ЛОЖЬ;
	Длительность = ТекущаяДата() - ДатаНачала;
	Консоль.ВидимостьКурсора(Истина);
	Консоль.ЦветТекста = ЦветТекстаКонсоли;
	Консоль.ВывестиСтроку("");
	Консоль = Неопределено;
	
КонецПроцедуры // Завершить

// ДлительностьВыполнения
//	Возвращает время, прошедшее с момента начала работы. Отсечка времени идет на момент последнего шага / либо завершения
//
// Параметры:
//  ВВидеПредставления - Булево - Признак возврата времени в человеко-читаемом строковом виде, иначе число 
//
// Возвращаемое значение:
//   Число, Строка - Время, прошедшее с начала работы
//
Функция ДлительностьВыполнения(ВВидеПредставления = ЛОЖЬ) Экспорт
	
	Если ВВидеПредставления Тогда

		Возврат ВремяВСтроку(Длительность);

	Иначе

		Возврат Длительность;

	КонецЕсли;

КонецФункции // ДлительностьВыполнения()

///////////////////////////////////////////////////////////////////////////////

Функция ВремяВСтроку(Знач Время)
	
	ВремяСтрока = "";
	Если Время > 86400 Тогда
		
		ВремяСтрока = ВремяСтрока + Цел(Время / 86400) + "д ";
		Время = Время % 86400;
		
	КонецЕсли;
	
	Если Время > 3600 Тогда
		
		ВремяСтрока = ВремяСтрока + Цел(Время / 3600) + "ч ";
		Время = Время % 3600;
		
	КонецЕсли;
	
	Если Время > 60 Тогда
		
		ВремяСтрока = ВремяСтрока + Цел(Время / 60) + "м ";
		Время = Время % 60;
		
	КонецЕсли;
	
	ВремяСтрока = ВремяСтрока + Цел(Время) + "с";
	
	Возврат ВремяСтрока;
	
КонецФункции // ВремяВСтроку()

Инициализирован = ЛОЖЬ;
Длительность = 0;