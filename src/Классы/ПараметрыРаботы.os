
Перем Лог Экспорт;
Перем Замер Экспорт;
Перем УправлениеСеансами Экспорт;
Перем Конфигуратор Экспорт;
Перем Параметры Экспорт;
Перем ПараметрыДеплойки Экспорт;
Перем ИспользоватьДеплойку Экспорт;

Перем РежимТестированияПараметров Экспорт;

Процедура ПолучитьПараметры( Знач пАргументы )
	
	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	
	Парсер.ДобавитьПараметр("ПутьКФайлу");
	Парсер.ДобавитьПараметрФлаг("-debug");
	Парсер.ДобавитьПараметрФлаг("-testparam");
	
	Параметры = Парсер.Разобрать(пАргументы);
	
	Если Параметры["-debug"] = Истина Тогда
		
		Лог.УстановитьУровень(УровниЛога.Отладка);
		
	КонецЕсли;

	РежимТестированияПараметров = Ложь;
	Если Параметры["-testparam"] = Истина Тогда
		
		РежимТестированияПараметров = Истина;
		Лог.Информация( "Включен режим тестирования параметров" );
		
	КонецЕсли;
	
	ошибкиЧтения = Неопределено;

	массивФайловНастроек = Новый Массив;
	массивФайловНастроек.Добавить( Неопределено );

	списокФайловСПараметрами = Параметры["ПутьКФайлу"];

	Если Не списокФайловСПараметрами = Неопределено Тогда	
		Для каждого цФайл Из СтрРазделить( списокФайловСПараметрами, ";") Цикл
			массивФайловНастроек.Добавить( цФайл );
		КонецЦикла;
	КонецЕсли;

	Параметры = ЧтениеПараметров.Прочитать( массивФайловНастроек, ошибкиЧтения);
	
	Для каждого цЭлемент Из ошибкиЧтения Цикл
		
		Лог.Ошибка( "Ошибка чтения файла " + цЭлемент.Ключ + ": " + цЭлемент.Значение );

	КонецЦикла;
	
	Для каждого цЭлемент Из Параметры Цикл
		
		Лог.Отладка( "Прочитан параметр " + цЭлемент.Ключ + ": " + цЭлемент.Значение );

	КонецЦикла;
	
КонецПроцедуры

Процедура Инициализация( Знач пАргументы, Знач пЛог ) Экспорт

	Замер = Новый Замер();
	
	Лог = пЛог;

	Лог.УстановитьРаскладку(Замер);

	Замер.НачатьЗамер();
	
	Замер.НачатьЗамер( "Инициализация" );
	
	ПолучитьПараметры( пАргументы );	
	
	Если ЗначениеЗаполнено( Параметры["slack_WebhookURL"] ) Тогда

		ОтправкаВSlack = Новый ОтправкаВSlack;
		ОтправкаВSlack.Инициализировать( Параметры["slack_WebhookURL"], Параметры["slack_channel"], Параметры["slack_username"], Параметры["slack_icon_emoji"]);

		Замер.УстановитьОтправкуВSlack( ОтправкаВSlack );

		Если ЗначениеЗаполнено( Параметры["slack_StartMessage"] ) Тогда

			Замер.ОтправитьВSlack( Параметры["slack_StartMessage"] );

		КонецЕсли;

	КонецЕсли;

	Если УдалятьСоединения()
		И Не ЭтоДинамическоеОбновление() Тогда
		
		ПараметрыДеплойки = Новый Соответствие;
		
		ПараметрыДеплойки.Вставить( "-ras", Параметры["Cluster.ras"] );
		ПараметрыДеплойки.Вставить( "-rac", Параметры["EXERAC"] );
		
		ПараметрыДеплойки.Вставить( "-db", Параметры["Base.Base"] );
		ПараметрыДеплойки.Вставить( "-db-user", Параметры["Base.User"] );
		ПараметрыДеплойки.Вставить( "-db-pwd", Параметры["Base.Password"] );
		
		ПараметрыДеплойки.Вставить( "-cluster-admin", Параметры["Cluster.Admin"] );
		ПараметрыДеплойки.Вставить( "-cluster-pwd", Параметры["Cluster.Password"] );
		
		ПараметрыДеплойки.Вставить( "-v8version", Параметры["v8version"] );
		ПараметрыДеплойки.Вставить( "-lockuccode", Параметры["Cluster.lockuccode"] );
		ПараметрыДеплойки.Вставить( "-lockmessage", Параметры["Cluster.lockmessage"] );
		ПараметрыДеплойки.Вставить( "-lockstart", Параметры["Cluster.lockstart"] );
		ПараметрыДеплойки.Вставить( "-lockstartat", Параметры["Cluster.lockstartat"] );
		
		ПараметрыДеплойки.Вставить( "Действие", Неопределено );

		УправлениеСеансами = Новый КомандаУправлениеСеансами;
		
		логДеплойки = Логирование.ПолучитьЛог("vanessa.app.deployka");
		логДеплойки.УстановитьУровень( Лог.Уровень() );
		замерПотомок = Замер.ПолучитьПотомка();
		логДеплойки.УстановитьРаскладку( замерПотомок );
		
	КонецЕсли;

	Конфигуратор = Новый УправлениеКонфигуратором;
	
	логРаннер = Логирование.ПолучитьЛог("oscript.lib.v8runner");
	логРаннер.УстановитьУровень( Лог.Уровень() );
	замерПотомок = Замер.ПолучитьПотомка();
	логРаннер.УстановитьРаскладку( замерПотомок );

	Конфигуратор.УстановитьКонтекст(Параметры["Base.Connect"], Параметры["Base.User"], Параметры["Base.Password"]);
	Конфигуратор.ПутьКПлатформе1С( Параметры["EXE1CV8"] );
	Конфигуратор.УстановитьКлючРазрешенияЗапуска( Параметры["Cluster.lockuccode"] );

	Замер.СообщитьЗамер( "Инициализация выполнена" );
	
КонецПроцедуры

Функция ЗначениеПараметраБулево( Знач пИмяПараметра, Знач пЗначениеПоУмолчанию = Ложь )

	значениеПараметра = Параметры[пИмяПараметра];

	Если значениеПараметра = Неопределено Тогда
		Возврат пЗначениеПоУмолчанию;
	КонецЕсли;

	Если значениеПараметра = Истина
		ИЛИ значениеПараметра = 1
		ИЛИ ВРег( значениеПараметра ) = ВРег( "Истина" )
		ИЛИ ВРег( значениеПараметра ) = ВРег( "true" ) Тогда
		Возврат Истина;
	ИначеЕсли значениеПараметра = Ложь
		ИЛИ значениеПараметра = 0
		ИЛИ ВРег( значениеПараметра ) = ВРег( "Ложь" )
		ИЛИ ВРег( значениеПараметра ) = ВРег( "false" ) Тогда
		Возврат Ложь;
	Иначе
		Возврат пЗначениеПоУмолчанию;
	КонецЕсли;

КонецФункции

Функция УдалятьСоединения() Экспорт	
	Возврат ИспользоватьДеплойку
	И ЗначениеПараметраБулево( "Cluster.UseLock", Истина );
КонецФункции

Функция ЭтоДинамическоеОбновление() Экспорт
	Возврат ЗначениеПараметраБулево( "UseDynamicUpdate" );
КонецФункции

Функция ОбновлятьКонфигурацию() Экспорт
	Возврат ЗначениеПараметраБулево( "UpdateCfg", Истина );
КонецФункции

Функция РебутатьСервер1С() Экспорт

	Возврат ЗначениеПараметраБулево( "RebootServer1C" );
	
КонецФункции

Функция ВыполнятьБекап() Экспорт

	Возврат ЗначениеПараметраБулево( "Source_SQL.UseBackup", Истина );
	
КонецФункции

Функция ПредставлениеБазы() Экспорт
	Возврат Параметры["Base.Connect"];
КонецФункции

Процедура ТестПараметров() Экспорт
	
	результат = Истина;

	Если ВыполнятьБекап() Тогда
		
		результат = Тест_SQL() И результат;
		
	КонецЕсли;

	Если ЗначениеПараметраБулево( "Source_SQL.UseBackup" ) Тогда
		
		результат = Тест_SQL("Source_SQL") И результат;
		
	КонецЕсли;

	результат = Тест_RAS() И результат;
	результат = Тест_Хранилище() И результат;

	Если Не результат Тогда
		
		Замер.СообщитьКритическуюОшибку( "Тест параметров провален" );
		ЗавершитьРаботу(1);
		
	КонецЕсли;

КонецПроцедуры

Функция Тест_SQL(Знач пИмяГруппыНастроек = "SQL")

	сервер       = Параметры[ пИмяГруппыНастроек + ".Server"];
	пользователь = Параметры[ пИмяГруппыНастроек + ".User"];
	пароль       = Параметры[ пИмяГруппыНастроек + ".Password"];
	имяБазы      = Параметры[ пИмяГруппыНастроек + ".Base"];

	Если сервер = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	представлениеПодключения = сервер + "/" + имяБазы;

	Замер.НачатьЗамер( "Тест подключения к SQL " + представлениеПодключения, "КоличествоСоединений" );

	проверкаСоединения = Новый РаботаСSQL();
	
	проверкаСоединения.ИнициализироватьЛог( Лог.Уровень(), Замер.ПолучитьПотомка() );

	проверкаСоединения.УстановитьСервер(       сервер );
	проверкаСоединения.УстановитьПользователя( пользователь );
	проверкаСоединения.УстановитьПароль(       пароль );
	проверкаСоединения.УстановитьИмяБазы(      имяБазы );
	
	Попытка
		количествоСоединений = проверкаСоединения.ПолучитьКоличествоСоединений();
		тестВыполнен = КоличествоСоединений >= 0;
	Исключение
		Лог.Ошибка( ПодробноеПредставлениеОшибки( ИнформацияОбОшибке()) );
	КонецПопытки;
	
	Если тестВыполнен Тогда
		
		Замер.СообщитьЗамер( "Тест подключения к SQL " + представлениеПодключения + " выполнен");
		Возврат Истина;
		
	Иначе

		Замер.СообщитьКритическуюОшибку( "Тест подключения к SQL " + представлениеПодключения + " провален" );

		Возврат Ложь;

	КонецЕсли;	

КонецФункции
 
Функция Тест_Хранилище()
	
	Замер.НачатьЗамер( "Тест подключения к хранилищу " + Параметры["Repo.Connect"], "ТестХранилища" );
	
	времФайлОтчета = ПолучитьИмяВременногоФайла();
	
	Попытка
		
		Конфигуратор.ПолучитьОтчетПоВерсиямИзХранилища(
			Параметры["Repo.Connect"], 
			Параметры["Repo.User"], 
			Параметры["Repo.Password"], 
			времФайлОтчета,
			100000,
			100000 );
		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;
		
		ОбщегоНазначения.УдалитьФайлЕслиСуществует( Конфигуратор.ФайлИнформации() );
		ОбщегоНазначения.УдалитьФайлЕслиСуществует( времФайлОтчета );

		Замер.СообщитьЗамер( "Тест подключения к хранилищу " + Параметры["Repo.Connect"] + " выполнен" );
		Возврат Истина;
		
	Исключение

		Замер.СообщитьКритическуюОшибку( "Тест подключения к хранилищу " + Параметры["Repo.Connect"] + " провален" );
		Замер.СообщитьКритическуюОшибку( ПодробноеПредставлениеОшибки( ИнформацияОбОшибке()) );

		Возврат Ложь;

	КонецПопытки;
	
КонецФункции

Функция Тест_RAS()
	
	Если Не УдалятьСоединения()
		ИЛИ ЭтоДинамическоеОбновление() Тогда

		Возврат Истина;

	КонецЕсли;

	Замер.НачатьЗамер( "Тест RAS", "тестRAS" );	

	Попытка

		ПараметрыДеплойки.Вставить("Действие", "unlock");
		количество = УправлениеСеансами.КоличествоСеансов( ПараметрыДеплойки );
		Замер.СообщитьЗамер( "Тест RAS выполнен" );
		Возврат количество >= 0;

	Исключение
		
		Замер.СообщитьКритическуюОшибку( "Тест RAS провален" );
		Замер.СообщитьКритическуюОшибку( ПодробноеПредставлениеОшибки( ИнформацияОбОшибке()) );
		Возврат Ложь;

	КонецПопытки;	

КонецФункции

ИспользоватьДеплойку = Ложь;